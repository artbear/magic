
Функция СохраняемыеРеквизиты()
	Структура = Новый Структура;
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("РеквизитОбработки");
	Структура.Вставить("РеквизитыОбъекта", Реквизиты);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ТабличнаяЧастьОбработки");
	Структура.Вставить("ТабличныеЧастиОбъекта", Реквизиты);
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("РеквизитФормы");
	Структура.Вставить("РеквизитыФормы", Реквизиты);
	
	Возврат Структура;
	
КонецФункции

Процедура СохранитьНастройкиВФайл(ПутьКФайлу, Объект, Форма) Экспорт
	СтруктураСохраняемыхРеквизитов = СохраняемыеРеквизиты();
	
	Реквизиты = СтруктураСохраняемыхРеквизитов.РеквизитыОбъекта;
	Для каждого КлючИЗначение из Реквизиты Цикл
		Значение = Объект[КлючИЗначение.Ключ];
		Реквизиты.Вставить(КлючИЗначение.Ключ, Значение);
	КонецЦикла;
	
	Реквизиты = СтруктураСохраняемыхРеквизитов.РеквизитыФормы;
	Для каждого КлючИЗначение из Реквизиты Цикл
		Значение = Форма[КлючИЗначение.Ключ];
		Реквизиты.Вставить(КлючИЗначение.Ключ, Значение);
	КонецЦикла;
	
	Реквизиты = СтруктураСохраняемыхРеквизитов.ТабличныеЧастиОбъекта;
	Для каждого КлючИЗначение из Реквизиты Цикл
		Значение = Объект[КлючИЗначение.Ключ].Выгрузить();
		Реквизиты.Вставить(КлючИЗначение.Ключ, Значение);
	КонецЦикла;
	
	Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
	ЗаписьXML = Новый ЗаписьXML; 
	ЗаписьXML.ОткрытьФайл(ПутьКФайлу);
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	Сериализатор.ЗаписатьXML(ЗаписьXML, СтруктураСохраняемыхРеквизитов);
	
КонецПроцедуры

Процедура ПрочитатьНастройкиИзФайла(ПутьКФайлу, Объект, Форма) Экспорт
	
	ЧтениеXML= Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПутьКФайлу);

	Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
	НастройкиИзФайла = Сериализатор.ПрочитатьXML(ЧтениеXML);
	
	Реквизиты = НастройкиИзФайла.РеквизитыОбъекта;
	ЗаполнитьЗначенияСвойств(Объект, Реквизиты);
	
	Реквизиты = НастройкиИзФайла.РеквизитыФормы;
	ЗаполнитьЗначенияСвойств(Форма, Реквизиты);
	
	ТабличныеЧастиСохраненные = НастройкиИзФайла.ТабличныеЧастиОбъекта;
	Для каждого ТабличнаяЧасть из Объект.Метаданные().ТабличныеЧасти Цикл
		ТабличнаяЧастьСохраненная = Неопределено;
		Если НЕ ТабличныеЧастиСохраненные.Свойство(ТабличнаяЧасть.Имя, ТабличнаяЧастьСохраненная) Тогда
			Продолжить;
		КонецЕсли;
		
		Объект[ТабличнаяЧасть.Имя].Загрузить(ТабличнаяЧастьСохраненная);
	КонецЦикла;
	
КонецПроцедуры


