
Перем мДекоратор;

#Область _// ПРЕДОПРЕДЕЛЕННЫЕ МЕТОДЫ

Функция ПолучитьСписокТестов(Браузер) Экспорт
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_ОбновитьКонфигурациюИзХранилища");
	ВсеТесты.Добавить("ТестДолжен_ОбновитьКонфигурациюБазыДанных");
	ВсеТесты.Добавить("ТестДолжен_Запустить1С_И_внешнююОбработку");
	ВсеТесты.Добавить("ТестДолжен_Запустить1С_И_ВнутреннююОбработку");

	Возврат ВсеТесты;
КонецФункции

Процедура Инициализация(БраузерОбъект) Экспорт
	
	мДекоратор = БраузерОбъект.ПолучитьКонтекст("Декоратор");
	
КонецПроцедуры

Функция РазрешенСлучайныйПорядокВыполненияТестов() Экспорт
	Возврат Ложь;
КонецФункции



Процедура ПередЗапускомНабораТестов() Экспорт
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт
	
	
	НачатьТранзакцию();
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
	Если ТранзакцияАктивна() Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗапускаНабораТестов() Экспорт
КонецПроцедуры
	
#КонецОбласти



#Область _// ТЕСТЫ

Процедура ТестДолжен_ОбновитьКонфигурациюИзХранилища() Экспорт
	
	Команда = "%Приложение1С%   DESIGNER   %КаталогИБ_Пользователь_Пароль% "
		+" %КаталогХранилища_Пользователь_Пароль% "
		+"/ConfigurationRepositoryUpdateCfg -force";
	ВыполнитьКоманду(Команда);
	
КонецПроцедуры
	
Процедура ТестДолжен_ОбновитьКонфигурациюБазыДанных() Экспорт
	
	Команда = "%Приложение1С%   DESIGNER   %КаталогИБ_Пользователь_Пароль% "
		+"  /UpdateDBCfg ";
	ВыполнитьКоманду(Команда);
	
КонецПроцедуры

Процедура ТестДолжен_Запустить1С_И_внешнююОбработку() Экспорт
	
	Команда = "%Приложение1С%   ENTERPRISE   %КаталогИБ_Пользователь_Пароль% "
		+" %ПутьКВнешнейОбработке%";
	ВыполнитьКоманду(Команда);
	
КонецПроцедуры

Процедура ТестДолжен_Запустить1С_И_ВнутреннююОбработку() Экспорт
	Команда = "%Приложение1С%   ENTERPRISE   %КаталогИБ_Пользователь_Пароль% "
		+"  /DisableStartupMessages %ПутьКВнутреннейОбработке%";
	ВыполнитьКоманду(Команда);
КонецПроцедуры

Процедура ВыполнитьКоманду(Команда)
	
	ПараметрыПодключения = мДекоратор.ТестовыеДанные("ПараметрыПодключения.Параметры");
	
	Для каждого Строка из ПараметрыПодключения Цикл
		Команда = СтрЗаменить(Команда, "%"+Строка.Параметр+"%", Строка.ЗначениеПараметра);
	КонецЦикла;
	
	КодВозврата = Неопределено;
	ЗапуститьПриложение(Команда, КаталогПрограммы(), Истина, КодВозврата);
	Если КодВозврата = Неопределено Тогда
		КодВозврата = "Неопределено";
	КонецЕсли;
	
	Сообщить("[Код возврата: " + КодВозврата + "] : " + Команда);
	
КонецПроцедуры

#КонецОбласти

